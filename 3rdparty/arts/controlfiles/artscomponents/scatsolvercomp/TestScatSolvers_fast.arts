#DEFINITIONS:  -*-sh-*-
#
# As TestScatSolvers.arts but does only inclue DISORT and RT4, as well as a
#single frequency, so it can be included as a fast test.
#
Arts2 {

INCLUDE "general/general.arts"
INCLUDE "general/continua.arts"
INCLUDE "general/agendas.arts"
INCLUDE "general/planet_earth.arts"

# Agenda for scalar gas absorption calculation
Copy(abs_xsec_agenda, abs_xsec_agenda__noCIA)

# on-the-fly absorption
Copy( propmat_clearsky_agenda, propmat_clearsky_agenda__OnTheFly )

# Blackbody surface
Copy( surface_rtprop_agenda, surface_rtprop_agenda__Blackbody_SurfTFromt_field )
VectorSet( surface_scalar_reflectivity, [0] )

# Standard ppath calculations
Copy( ppath_step_agenda, ppath_step_agenda__GeometricPath )
Copy( ppath_agenda, ppath_agenda__FollowSensorLosPath )

# Radiative transfer agendas
Copy( iy_main_agenda, iy_main_agenda__Emission )
Copy( iy_space_agenda, iy_space_agenda__CosmicBackground )
Copy( iy_surface_agenda, iy_surface_agenda__UseSurfaceRtprop )
Copy( iy_cloudbox_agenda,  iy_cloudbox_agenda__QuarticInterpField )

# Absorption species
abs_speciesSet( species=[ "N2-SelfContStandardType",
                          "O2-PWR93",
                          "H2O-PWR98"                          
                        ] )

# No line data needed here
abs_lines_per_speciesSetEmpty

# Dimensionality of the atmosphere
AtmosphereSet1D

# Brigtness temperatures used
StringSet( iy_unit, "PlanckBT" )

# Various things not used
ArrayOfStringSet( iy_aux_vars, [] )
jacobianOff

# Read data created by setup_test.m
ReadXML( p_grid,                  "testdata/p_grid.xml" )
ReadXML( t_field,                 "testdata/t_field.xml" )
ReadXML( z_field,                 "testdata/z_field.xml" )
ReadXML( vmr_field,               "testdata/vmr_field.xml" )
ReadXML( particle_bulkprop_field, "testdata/particle_bulkprop_field" )
ReadXML( particle_bulkprop_names, "testdata/particle_bulkprop_names" )
ReadXML( scat_data_raw,           "testdata/scat_data.xml" )
ReadXML( scat_meta,               "testdata/scat_meta.xml" )

# Define hydrometeors
#
StringCreate( species_id_string )
#
# Scat species 0
StringSet( species_id_string, "RWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "RWC" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dveq" )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdWangEtAl16( t_min = 273, t_max = 999 )
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )
#
# Scat species 1
StringSet( species_id_string, "IWC" )
ArrayOfStringSet( pnd_agenda_input_names, [ "IWC" ] )
ArrayOfAgendaAppend( pnd_agenda_array ){
  ScatSpeciesSizeMassInfo( species_index=agenda_array_index, x_unit="dveq",
                           x_fit_start=100e-6 )
  Copy( psd_size_grid, scat_species_x )
  Copy( pnd_size_grid, scat_species_x )
  psdMcFarquaharHeymsfield97( t_min = 10, t_max = 273, t_min_psd = 210 )
  pndFromPsdBasic
}
Append( scat_species, species_id_string )
Append( pnd_agenda_array_input_names, pnd_agenda_input_names )



# Special settings for the scattering solvers
#
# Angular grid DISORT
DOAngularGridsSet( N_za_grid=38 )
# RT4 creates own grirs, so we need copies of the ones created above
VectorCreate( za_grid_copy )
VectorCreate( aa_grid_copy )
#
# Hybrid requires that ppath_lmax is not too high
NumericSet( ppath_lmax, 100 )
AgendaCreate( iy_hybrid_agenda )
AgendaSet( iy_hybrid_agenda ){
  Ignore( iy_id )
  ppathCalc( cloudbox_on = 0 ) 
  iyHybrid
  # The line below is just temporary
  Touch( iy_aux)  
}

# Versions of y for various calculations
VectorCreate( y_disort )
VectorCreate( y_rt4 )
VectorCreate( y_hybrid )
#
StringCreate( message )

# Perform some basic checks
abs_xsec_agenda_checkedCalc
lbl_checkedCalc
propmat_clearsky_agenda_checkedCalc
atmfields_checkedCalc( bad_partition_functions_ok = 1 )


# Intitial settings for tests
IndexSet( stokes_dim, 1 )
# See non-fast cfile for possible frequencies to select
VectorSet( f_grid, [165e9] )
Extract( z_surface, z_field, 0 )
MatrixSet( sensor_pos, [20e3;8e3;20e3] )
MatrixSet( sensor_los, [180;160;130] )

# Some stuff that depends on the settings above
sensorOff
atmgeom_checkedCalc
sensor_checkedCalc
scat_dataCalc
scat_data_checkedCalc
#
VectorExtractFromMatrix( rtp_pos, z_surface, 0, "row" )
InterpAtmFieldToPosition( out=surface_skin_t, field=t_field )


# Test 1: Cloubox with no scattering
# ---------------------------------------------------------------------
cloudboxSetFullAtm
pnd_fieldZero
cloudbox_checkedCalc
#
StringSet( message, "No scattering" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
INCLUDE "run_hybrid.arts"
Print( y_hybrid, 0 )
#
Compare( y_rt4, y_hybrid, 0.02, "Zero particles, RT4" )
Compare( y_disort, y_hybrid, 0.02, "Zero particles, DISORT" )


# Test 2: With nominal RWC/IWC and surface at p_grid[0]
# ---------------------------------------------------------------------
pnd_fieldCalcFromParticleBulkProps
cloudbox_checkedCalc
#
StringSet( message, "Nominal case" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
#
Compare( y_rt4, y_disort, 0.2, "Test2, RT4 vs. DISORT" )


# Test 3: As 2 but with RWC/IWC increased with a factor of 3
# ---------------------------------------------------------------------
Tensor4Scale( particle_bulkprop_field, particle_bulkprop_field, 3 )
pnd_fieldCalcFromParticleBulkProps
#
StringSet( message, "Increased RWC/IWC" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
#
Compare( y_rt4, y_disort, 0.2, "Test3, RT4 vs. DISORT" )


# Test 4: As 2 but with surface moved upwards
# ---------------------------------------------------------------------
Tensor4Scale( particle_bulkprop_field, particle_bulkprop_field, 0.3333333 )
pnd_fieldCalcFromParticleBulkProps
MatrixAddScalar( z_surface, z_surface, 3000 )
VectorExtractFromMatrix( rtp_pos, z_surface, 0, "row" )
InterpAtmFieldToPosition( out=surface_skin_t, field=t_field )
##
StringSet( message, "Surface moved upwards" )
Print( message, 0 )
#
INCLUDE "run_rt4.arts"
Print( y_rt4, 0 )
INCLUDE "run_disort.arts"
Print( y_disort, 0 )
#
Compare( y_rt4, y_disort, 0.2, "Test4, RT4 vs. DISORT" )

}
 
